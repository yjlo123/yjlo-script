/* HeapList
 * @author Liu Siwei (liusiwei.yjlo@gmail.com)
 * A heap that works like an array list
 */

import Heap;

func HeapList() extends Heap {

	var _size = 0;
	_root = _Node();

	func init(lst) {
		_size = 0;
		var i = 0;
		while !$is_empty(lst) {
			put(i, $head(lst));
			i++;
			lst = $tail(lst);
		}
	}

	func put(index, value) {
		_getNodeByPath(_getPathByIndex(index), true).value = value;
		if index + 1 > _size {
			_size = index + 1;
		}
	}

	func get(index) {
		index /= 1;
		if !(index >= 0) { return null; }
		var target = _getNodeByPath(_getPathByIndex(index), false);
		if target {
			return target.value;
		} else {
			return null;
		}
	}

	func size() {
		return _size;
	}
	
	func clear() {
		_root = _Node();
		_size = 0;
	}
	
	func toList() {
		var list = [];
		for i in size()-1...0 by -1 {
			list = $pair(get(i), list);
		}
		return list;
	}
	
	func toString() {
		var str = "[";
		for i in 0..<size()-1 {
			str += (get(i) + ", ");
		}
		if size() > 0 {
			str += (get(size()-1) + "]");
		}
		return str;
	}
	
	return func(){};
}

/*
var arr = HeapList();

arr.init(["zero", "one", "two", "three", "four", "five", "six"]);
arr.put(7, "seven");
print(arr.toList());
print(arr.size());
print(arr.get(5));
*/
