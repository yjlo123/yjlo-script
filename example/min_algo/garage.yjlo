/*
There is a parking lot with only one empty spot. Given the initial state
of the parking lot and the final state. Each step we are only allowed to
move a car out of its place and move it into the empty spot.
The goal is to find out the least movement needed to rearrange
the parking lot from the initial state to the final state.

Say the initial state is an array:
[1,2,3,0,4],
where 1,2,3,4 are different cars, and 0 is the empty spot.

And the final state is
[0,3,2,1,4].
We can swap 1 with 0 in the initial array to get [0,2,3,1,4] and so on.
Each step swap with 0 only.
*/

import ListUtil;

len := ListUtil.len;
equal := ListUtil.equal;
get := ListUtil.get;
set := ListUtil.set;
index := ListUtil.index;

func swap(lst, a, b) {
	temp := get(lst, a);
	lst = set(lst, a, get(lst, b));
	lst = set(lst, b, temp);
	return lst;
}

func garage(beg, end) {
	i := 0;
	moves := 0;
	if len(beg) != len(end) { return; }
	print(beg);
	while !equal(beg, end) {
		if get(beg, i) != 0 && get(beg, i) != get(end, i) {
			empty := index(beg, 0);
			final_pos := index(end, get(beg, i));

			if empty != final_pos {
				beg = swap(beg, final_pos, empty);
				print(beg);
				empty = index(beg, 0);
				beg = swap(beg, i, empty);
				print(beg);
				moves += 2;
			} else {
				beg = swap(beg, i, empty);
				print(beg);
				moves++;
			}
		}
		i++;
		if i == len(beg) {
			i = 0;
		}
	}
	return moves;
}

print(garage([1,2,3,0,4], [0,3,2,1,4]));
