/* LeetCode 4. Median of Two Sorted Arrays
There are two sorted arrays nums1 and nums2 of size m and n respectively.

Find the median of the two sorted arrays. The overall run time complexity should be O(log (m+n)).

Example 1:
nums1 = [1, 3]
nums2 = [2]
The median is 2.0

Example 2:
nums1 = [1, 2]
nums2 = [3, 4]
The median is (2 + 3)/2 = 2.5
*/
import HeapList;
import Math;

func median(listA, listB){
	// convert list to HeapList
	A := HeapList();
	A.init(listA);
	B := HeapList();
	B.init(listB);
	
	m := A.size();
	n := B.size();
	if m > n { return median(B, A); }
	if n == 0 { throw("Value Error"); }
	
	var imin = 0, imax = m;
	half_len := (m + n + 1) / 2;

	var i, j, max_of_left, min_of_right;
	while imin <= imax {
		i = (imin + imax) / 2;
		j = half_len - i;
		if i < m && B.get(j-1) > A.get(i) {
			// i is too small, must increase it
			imin = i + 1;
		} else if i > 0 && A.get(i-1) > B.get(j) {
			// i is too big, must decrease it
			imax = i - 1;
		} else {
			// i is perfect

			if i == 0 { max_of_left = B.get(j-1); }
			else if j == 0 { max_of_left = A.get(i-1); }
			else { max_of_left = Math.max(A.get(i-1), B.get(j-1)); }

			if (m + n) % 2 == 1 {
				return max_of_left;
			}

			if i == m { min_of_right = B.get(j); }
			else if j == n { min_of_right = A.get(i); }
			else { min_of_right = Math.min(A.get(i), B.get(j)); }

			return (max_of_left + min_of_right) /. 2;
		}
	}
}

print(median([1,2], [3,4]));
